// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/b/RAM512.hdl
/**
 * Memory of 512 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    PARTS:
    DMux8Way(in=load, sel=address[6..8], a=first, b=second, c=third, d=fourth, e=fifth, f=sixth, g=seventh, h=eighth);

    RAM64(in=in, load=first, address=address[0..5], out=out1);
    RAM64(in=in, load=second, address=address[0..5], out=out2);
    RAM64(in=in, load=third, address=address[0..5], out=out3);
    RAM64(in=in, load=fourth, address=address[0..5], out=out4);
    RAM64(in=in, load=fifth, address=address[0..5], out=out5);
    RAM64(in=in, load=sixth, address=address[0..5], out=out6);
    RAM64(in=in, load=seventh, address=address[0..5], out=out7);
    RAM64(in=in, load=eighth, address=address[0..5], out=out8);

    Mux8Way16(a=out1, b=out2, c=out3, d=out4, e=out5, f=out6, g=out7, h=out8, sel=address[6..8], out=out);
}
